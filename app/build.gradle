apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
        config {
            keyAlias 'playStoreSigningKey'
            keyPassword AttendanceTrackerKeyStorePassword
            storeFile file('../release-documents/Attendance-Tracker.jks')
            storePassword AttendanceTrackerKeyStorePassword
        }
    }
    compileSdkVersion 26

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.varunbarad.attendancetracker"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField 'String', "InMobiAccountId", AttendanceTracker_InMobiAccountId
            buildConfigField 'String', "InMobiPlacementIdMainAttendance", AttendanceTracker_InMobiPlacementIdMainAttendance
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "InMobiAccountId", AttendanceTracker_InMobiAccountId
            buildConfigField 'String', "InMobiPlacementIdMainAttendance", AttendanceTracker_InMobiPlacementIdMainAttendance
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions 'application'

    productFlavors {
        free {
            applicationId 'com.varunbarad.attendancetracker'
            dimension 'application'
        }
    }
}

ext {
    multiDexVersion = '1.0.2'
    supportLibraryVersion = '26.1.0'
    constraintLayoutVersion = '1.0.2'
    calendarViewLibraryVersion = 'v1.1'
    materialDialogsVersion = '0.9.6.0'
    firebaseVersion = '11.4.2'
    retrofitVersion = '2.3.0'
    picassoVersion = '2.5.2'
    gsonVersion = '2.8.2'
    inMobiVersion = '7.0.0'

    jUnitVersion = '4.12'
    androidTestRunnerVersion = '1.0.1'
    espressoVersion = '3.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs/main', include: ['*.jar'])
    freeImplementation fileTree(dir: 'libs/free', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"

    // Android Data-Binding Annotation Processor for Kotlin
    kapt "com.android.databinding:compiler:${androidPluginVersion}"

    // Google Support Library for Multidex
    implementation "com.android.support:multidex:${multiDexVersion}"

    // Google Design Support libraries
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:preference-v7:${supportLibraryVersion}"
    implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    // Calendar View
    implementation "com.github.npanigrahy:Custom-Calendar-View:${calendarViewLibraryVersion}"

    // Material Dialogs
    implementation "com.afollestad.material-dialogs:core:${materialDialogsVersion}"

    // Google Play Services
    implementation "com.google.android.gms:play-services:${firebaseVersion}"

    //Firebase
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"

    // InMobi Ad Platform
    implementation "com.inmobi.monetization:inmobi-ads:${inMobiVersion}"

    // Used to load images from network
    implementation "com.squareup.picasso:picasso:${picassoVersion}"

    // Library used for JSON serialization/deserialization
    implementation "com.google.code.gson:gson:${gsonVersion}"

    // Retrofit to fetch and parse API responses
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"

    // JUnit Test Runner
    testImplementation "junit:junit:${jUnitVersion}"

    // Android specific test runner
    androidTestImplementation "com.android.support.test:runner:${androidTestRunnerVersion}"
    // Core espresso library for UI tests
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoVersion}"
}

repositories {
    mavenCentral()
}

kapt {
    generateStubs = true
}